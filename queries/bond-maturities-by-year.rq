PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qt3: <https://triples.beesondouglas.com/questrade/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX qt: <https://ontologies.beesondouglas.com/questrade/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qtx: <https://taxonomies.beesondouglas.com/questrade/>

SELECT 
?maturityyear
(SUM(?amt) AS ?peryear)

FROM qt3:__securities__latest
where {
    [] a gist:ScheduledEvent ;
        qt:plannedOccursDate ?repaydatetime ;
        gist:hasMagnitude [ gist:numericValue ?amt ] ;
        gist:isCharacterizedAs  qtx:_FinancialBusinessEventType_debt_principal_repayment .
    BIND(YEAR(?repaydatetime) AS ?maturityyear)

} GROUP BY ?maturityyear 
ORDER BY ASC(?maturityyear)