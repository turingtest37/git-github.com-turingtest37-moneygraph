PREFIX gist: <https://ontologies.semanticarts.com/gist/>
PREFIX skos: <http://www.w3.org/2004/02/skos#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qt: <https://ontologies.beesondouglas.com/questrade/>
PREFIX qtx: <https://taxonomies.beesondouglas.com/questrade/>
PREFIX qt3: <https://triples.beesondouglas.com/questrade/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>

CONSTRUCT
{

# Transaction Date,Settlement Date,Action,Symbol,Description,Quantity,Price,Gross Amount,Commission,Net Amount,Currency,Account #,Activity Type,Account Type
# 2014-03-03 12:00:00 AM,2014-03-03 12:00:00 AM,FXT,,CONVERSION - CAD/USD,0.00000,0.00000000,0.00,0.00,885.00,USD,51590610,FX conversion,Individual RRSP
# 2014-03-03 12:00:00 AM,2014-03-06 12:00:00 AM,Buy,OPXA,OPEXA THERAPEUTICS INC COM WE ACTED AS AGENT,500.00000,1.76000000,-880.00,-5.00,-885.00,USD,51590610,Trades,Individual RRSP
# 2015-03-16 12:00:00 AM,2015-03-16 12:00:00 AM,DIS,P040749,RTS OPEXA THERAPEUTICS INC EXP PENDING CONTRA CUSIP RTS DIST ON 500 SHS REC 03/13/15 PAY 03/13/15,500.00000,0.00000000,0.00,0.00,0.00,USD,51590610,Dividends,Individual RRSP
# 2015-04-24 12:00:00 AM,2015-04-24 12:00:00 AM,RTS,P040749,RTS OPEXA THERAPEUTICS INC CONTRA CUSIP EXP 04/08/2015 RIGHTS EXPIRED BOOK VALUE $ .00,-500.00000,0.00000000,0.00,0.00,0.00,USD,51590610,Corporate actions,Individual RRSP
# 2015-09-29 12:00:00 AM,2015-09-29 12:00:00 AM,REV,P032174,OPEXA THERAPEUTICS INC COM REVERSE SPLIT 1:8 INTO 68372T301,-500.00000,0.00000000,0.00,0.00,0.00,USD,51590610,Corporate actions,Individual RRSP

#  [] <urn:grossamtrnd> ?__gross_amount_rnd ;
#     <urn:grossamtrndmult100> ?__gross_amount_rnd_mult_100 ;
#     <urn:grossamtrndmult1000> ?__gross_amount_rnd_mult_1000 ;
#     <urn:qty_clean> ?__qty_clean ;
#     <urn:grossamt_clean> ?__grossamt_clean ;
#     .

#     <urn:txdt> ?__transaction_date ;
#     <urn:settledt> ?__settlement_date .
 [] 
 <urn:Quantity> ?Quantity ;
 <urn:qtyclean> ?__qty_clean ;
 <urn:qty> ?__qty_amount ;
 <urn:price> ?__price_amount ;
 <urn:gross> ?__gross_amount ;
 <urn:commission> ?__commission_amount ;
 <urn:net> ?__net_amount ;
 <urn:discriminator> ?__discriminator ;
<urn:symbol> ?__equity_symbol_str ;
<urn:symbol_norm> ?__symbol_str_norm ;
 <urn:nosymbol> ?__no_symbol ;
 <urn:eq_symbol> ?__eq_symbol ;
 .
 

    ?__activity a gist:Event .
    ?__activity skos:prefLabel ?__activity_label .
    ?__activity gist:description ?Description .
    ?__activity qt:transactionDate ?__transaction_date .
    ?__activity qt:settlementDate ?__settlement_date .
    ?__activity gist:isCharacterizedAs ?__event_type .

# Security account is linked to Security
    ?__security_account gist:isAffectedBy ?__activity .
    
# Dividend case
    ?__activity gist:hasMagnitude ?__dividend_magn .
    ?__dividend_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityShareDividendAmount ;
    .


# Purchase Case
    ?__activity gist:hasMagnitude ?__purchase_qty_magn .
    ?__purchase_qty_magn a gist:Magnitude ;
        gist:numericValue ?__qty_amount ;
        gist:hasUnitOfMeasure gist:_each ;
        gist:isCategorizedBy qtx:_Aspect_securityShareCount ;
    .

    ?__activity gist:hasMagnitude ?__purchase_price_magn .
    ?__purchase_price_magn a gist:Magnitude;
        gist:numericValue ?__price_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityShareUnitPriceAmount ;
    .

    ?__activity gist:hasMagnitude ?__purchase_gross_amt_magn .
    ?__purchase_gross_amt_magn a gist:Magnitude ;
        gist:numericValue ?__gross_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeGrossAmount ;
    .

    ?__activity gist:hasMagnitude ?__purchase_commission_magn .
    ?__purchase_commission_magn a gist:Magnitude ;
        gist:numericValue ?__commission_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeTransactionCommission ;
    .
    
    ?__activity gist:hasMagnitude ?__purchase_net_amt_magn .
    ?__purchase_net_amt_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeNetAmount ;
    .

# Sale Case
    ?__activity gist:hasMagnitude ?__sale_qty_magn .
    ?__sale_qty_magn a gist:Magnitude ;
        gist:numericValue ?__qty_amount ;
        gist:hasUnitOfMeasure gist:_each ;
        gist:isCategorizedBy qtx:_Aspect_securityShareCount ;
    .

    ?__activity gist:hasMagnitude ?__sale_price_magn .
    ?__sale_price_magn a gist:Magnitude;
        gist:numericValue ?__price_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityShareUnitPriceAmount ;
    .

    ?__activity gist:hasMagnitude ?__sale_gross_amt_magn .
    ?__sale_gross_amt_magn a gist:Magnitude ;
        gist:numericValue ?__gross_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeGrossAmount ;
    .

    ?__activity gist:hasMagnitude ?__sale_commission_magn .
    ?__sale_commission_magn a gist:Magnitude ;
        gist:numericValue ?__commission_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeTransactionCommission ;
    .
    
    ?__activity gist:hasMagnitude ?__sale_net_amt_magn .
    ?__sale_net_amt_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityTradeNetAmount ;
    .

# Dividend Case
    ?__activity gist:hasMagnitude ?__dividend_magn .
    ?__dividend_magn  a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_securityShareDividendAmount ;
    .

# Interest Case
    ?__activity gist:hasMagnitude ?__interest_magn.
    ?__interest_magn  a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_debtInterestAmount ;
    .

# Deposit Case
    ?__activity gist:hasMagnitude ?__deposit_magn .
    ?__deposit_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_depositAmount ;
    .

# Post-merger deposit case 
    ?__activity gist:hasMagnitude ?__merger_deposit_magn.
    ?__merger_deposit_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_depositAmount ;
    .

# Foreign currency exchanged
    ?__activity gist:hasMagnitude ?__fxt_magn .
    ?__fxt_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_foreignCurrencyExchangeAmount ;

# Debt (bond) Repayment
    ?__activity gist:hasMagnitude ?__debt_repayment_magn .
    ?__debt_repayment_magn a gist:Magnitude ;
        gist:numericValue ?__net_amount ;
        gist:hasUnitOfMeasure ?__event_currency ;
        gist:isCategorizedBy qtx:_Aspect_debtPrincipalRepaymentAmount ;

}

#### Fields

# Transaction Date
# ,Settlement Date
# ,Trade Date
# ,Action
# ,Symbol
# ,Description
# ,Quantity
# ,Price
# ,Gross Amount
# ,Commission
# ,Net Amount
# ,Currency
# ,Account #
# ,Activity Type
# ,Account Type


WHERE

{
   SERVICE <x-sparql-anything:> {

        BIND(IRI(CONCAT("file://",?_loc)) AS ?__location)

        fx:properties
            fx:location ?__location ;    
            fx:csv.headers true ;
            fx:blank-nodes false ;
            fx:csv.null-string "" ;
            fx:root ?__location ;
        .

        ?__csvline xyz:Transaction%20Date ?TransactionDate .
        ?__csvline xyz:Settlement%20Date ?SettlementDate .
        OPTIONAL { ?__csvline <http://sparql.xyz/facade-x/data/Trade%20Date> ?TradeDate }
        OPTIONAL { ?__csvline xyz:Action ?Action }
        OPTIONAL { ?__csvline xyz:Symbol ?Equity_Symbol }
        ?__csvline xyz:Description ?Description .
        ?__csvline xyz:Quantity ?Quantity .
        ?__csvline xyz:Price ?Price .
        ?__csvline <http://sparql.xyz/facade-x/data/Gross%20Amount> ?GrossAmount .
        ?__csvline xyz:Commission ?Commission .
        ?__csvline <http://sparql.xyz/facade-x/data/Net%20Amount> ?NetAmount .
        ?__csvline xyz:Currency ?Currency .
        ?__csvline <http://sparql.xyz/facade-x/data/Account%20%23> ?AccountNumber .
        ?__csvline <http://sparql.xyz/facade-x/data/Activity%20Type> ?ActivityType .
        OPTIONAL { ?__csvline <http://sparql.xyz/facade-x/data/Account%20Type> ?AccountType }

    }

# Security Symbol - remove leading ., or .zz if xxxxx.zz
    BIND(STR(?Equity_Symbol) AS ?__equity_symbol_str)
    BIND(IF(
        # TODO convert to string
        STRSTARTS(?__equity_symbol_str,"."), 
        STRAFTER(?__equity_symbol_str,"."),
        IF(CONTAINS(?__equity_symbol_str,"."),
            STRBEFORE(?__equity_symbol_str,"."),
            ?__equity_symbol_str)) AS ?__symbol_str_norm)

### Clean up the numbers

# Quantity
    BIND(REPLACE(REPLACE(STR(?Quantity),"[^0-9.]",""), "(\\.\\d{2})\\d*$", "$1") AS ?__qty_clean)
    BIND(ABS(xsd:integer(round(STRDT(?__qty_clean, xsd:decimal)))) AS ?__qty_amount)
# Price
# Need at least 3 significant digits
    BIND(REPLACE(REPLACE(STR(?Price),"[^0-9.]",""), "(\\.\\d{2,3})\\d*$", "$1") AS ?__price_clean)
    BIND(ABS(xsd:decimal(?__price_clean)) AS ?__price_amount)

# Gross Amount
    BIND(REPLACE(REPLACE(STR(?GrossAmount),"[^0-9.]",""), "(\\.\\d{2})\\d*$", "$1") AS ?__grossamt_clean)
    BIND(ABS(xsd:decimal(?__grossamt_clean)) AS ?__gross_amount)

# Commission
    BIND(REPLACE(REPLACE(STR(?Commission),"[^0-9.]",""), "(\\.\\d{2})\\d*$", "$1") AS ?__commamt_clean)
    BIND(ABS(xsd:decimal(?__commamt_clean)) AS ?__commission_amount)
# Net Amount 
    BIND(REPLACE(REPLACE(STR(?NetAmount),"[^0-9.]",""), "(\\.\\d{2})\\d*$", "$1") AS ?__netmamt_clean)
    BIND(ABS(xsd:decimal(?__netmamt_clean)) AS ?__net_amount)

# Discriminator - for use where no symbol exists in the data
# Sum of qty,price,gross,commission,net as a discrimator to create a unique Event IRI
    BIND(STR(round((?__qty_amount + ?__price_amount + ?__gross_amount + ?__commission_amount + ?__net_amount)*1000)/1000) AS ?__discriminator)

# Transaction Date - arghhhhhh (looks like "6/13/23 00:00" in the data)
    BIND(REPLACE(?TransactionDate,"^([0-9-]+).*$","$1T00:00:00") AS ?__tx_dt_str)
    BIND(STRDT(?__tx_dt_str, xsd:dateTime) AS ?__transaction_date)

#  Local part of Security IRI = the equity symbol = where no ticker symbol exists
    BIND(MD5(CONCAT(
                STR(?__transaction_date),
                ?__qty_clean,
                ?__discriminator
    )) AS ?__no_symbol)

# Equity Symbol
    BIND(COALESCE(UCASE(?__symbol_str_norm), ?__no_symbol) AS ?__eq_symbol)

# Account Number
    BIND(ENCODE_FOR_URI(LCASE(STR(?AccountNumber))) AS ?__acct_key)

# Settlement Date
    BIND(REPLACE(?SettlementDate,"^([0-9-]+).*$","$1T00:00:00") AS ?__settle_dt_str)
    BIND(STRDT(?__settle_dt_str, xsd:dateTime) AS ?__settlement_date)

# Action - default to Unknown if not yet modeled
BIND(
    IF(!BOUND(?Action) || UCASE(?Action)='DIV' || UCASE(?ActivityType)='DIVIDENDS',qtx:_FinancialBusinessEventType_dividendRemittance,
    IF(UCASE(?Action)='BUY',gist:_FinancialBusinessEventType_purchase,
    IF(UCASE(?Action)='SELL',gist:_FinancialBusinessEventType_sale,
    IF(UCASE(?Action)='INT',qtx:_FinancialBusinessEventType_interestRemittance,
    IF(UCASE(?Action)='CON',qtx:_FinancialBusinessEventType_deposit,
    IF(UCASE(?Action)='MGR',qtx:_FinancialBusinessEventType_deposit,
    IF(UCASE(?Action)='FTX',qtx:_FinancialBusinessEventType_exchangeCurrency,
    IF(UCASE(?Action)='LQD',gist:_FinancialBusinessEventType_financialTransfer,
    IF(UCASE(?Action)='LIQ',gist:_FinancialBusinessEventType_financialTransfer,
    IF(UCASE(?Action)='TFI',gist:_FinancialBusinessEventType_financialTransfer,
    IF(UCASE(?Action)='RED',gist:_FinancialBusinessEventType_debtPrincipalRepayment,
    qtx:_FinancialBusinessEventType_unknown)))))))))) AS ?__event_type)

# Marketable Security
    BIND(IRI(CONCAT(STR(qt3:),'_MarketableSecurity_', ?__eq_symbol)) AS ?__security)

# Security Account
    BIND(IRI(CONCAT(STR(qt3:),'_InvestmentAccount_',?__acct_key,'_',?__eq_symbol)) AS ?__security_account)

# Transaction key = convenience binding for making new IRIs
    BIND(CONCAT(
        ?__acct_key,'_',
        ?__eq_symbol,'_',
        REPLACE(STR(?__transaction_date),"\\W+","-"),'_',
        REPLACE(LCASE(?Action),"\\W+","-")
        ) AS ?__tx_key)

# Activity
    BIND(IRI(CONCAT(
        STR(qt3:),
        '_Event_',
        ?__tx_key,
        '_',
        ?__discriminator)) AS ?__activity)
        # )) AS ?__activity)

# Activity Label
    BIND(CONCAT(?Activity_Type, " ", ?__eq_symbol, " ", STR(?__transaction_date)) AS ?__activity_label)

### Dividends
# Only net amount counts
BIND(IF(?__event_type = qtx:_FinancialBusinessEventType_dividendRemittance, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_dividend_',?__tx_key,'_',?__netmamt_clean)), 
    ?unbound) AS ?__dividend_magn)

#### Buy
# Need all of Quantity,Price,Gross Amt,Commission,Net Amt
BIND(IF(?__event_type = gist:_FinancialBusinessEventType_purchase,
    IRI(CONCAT(STR(qt3:),'_Magnitude_purchaseQuantity_',?__tx_key,'_',?__qty_clean)),
    ?unbound) AS ?__purchase_qty_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_purchase,
    IRI(CONCAT(STR(qt3:),'_Magnitude_purchasePrice_',?__tx_key,'_',?__price_clean)),
    ?unbound) AS ?__purchase_price_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_purchase,
    IRI(CONCAT(STR(qt3:),'_Magnitude_purchaseGrossAmount_',?__tx_key,'_',?__grossamt_clean)),
    ?unbound) AS ?__purchase_gross_amt_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_purchase,
    IRI(CONCAT(STR(qt3:),'_Magnitude_commissionPaid_',?__tx_key,'_',?__commamt_clean)),
    ?unbound) AS ?__purchase_commission_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_purchase,
    IRI(CONCAT(STR(qt3:),'_Magnitude_purchaseNetAmount_',?__tx_key,'_',?__netmamt_clean)),
    ?unbound) AS ?__purchase_net_amt_magn)

### Sell
# Need all of Quantity,Price,Gross Amt,Commission,Net Amt
BIND(IF(?__event_type = gist:_FinancialBusinessEventType_sale,
    IRI(CONCAT(STR(qt3:),'_Magnitude_saleQuantity_',?__tx_key,'_',?__qty_clean)),
    ?unbound) AS ?__sale_qty_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_sale,
    IRI(CONCAT(STR(qt3:),'_Magnitude_salePrice_',?__tx_key,'_',?__price_clean)),
    ?unbound) AS ?__sale_price_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_sale,
    IRI(CONCAT(STR(qt3:),'_Magnitude_saleGrossAmount_',?__tx_key,'_',?__grossamt_clean)),
    ?unbound) AS ?__sale_gross_amt_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_sale,
    IRI(CONCAT(STR(qt3:),'_Magnitude_commissionPaid_',?__tx_key,'_',?__commamt_clean)),
    ?unbound) AS ?__sale_commission_magn)

BIND(IF(?__event_type = gist:_FinancialBusinessEventType_sale,
    IRI(CONCAT(STR(qt3:),'_Magnitude_purchaseNetAmount_',?__tx_key,'_',?__netmamt_clean)),
    ?unbound) AS ?__sale_net_amt_magn)

### Interest
# Only net amount counts
BIND(IF(?__event_type = qtx:_FinancialBusinessEventType_interestRemittance, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_interestReceived_',?__tx_key,'_',?__netmamt_clean)), 
    ?unbound) AS ?__interest_magn)

### Deposits
# Only net amount counts
BIND(IF(?__event_type = gist:_FinancialBusinessEventType_deposit, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_deposit_',?__tx_key,'_',?__netmamt_clean)), 
    ?unbound) AS ?__deposit_magn)

### Contributions
# same as deposits for now

### Mergers and acquisitions
# Only net amount counts
BIND(IF(?__event_type = qtx:_FinancialBusinessEventType_assetMerger && xsd:decimal(?Quantity) < 0, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_depositFromMerger_',?__tx_key,'_',?__netmamt_clean)), 
    ?unbound) AS ?__merger_deposit_magn)

# Currency exchanged
BIND(IF(?__event_type = gist:_FinancialBusinessEventType_exchangeCurrency, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_exchangedCurrency_',?__acct_key,'_',ENCODE_FOR_URI(STR(?__transaction_date)),'_',?__netmamt_clean)), 
    ?unbound) AS ?__fxt_magn)

# Debt Principal Repayment (i.e. 'redemption')
BIND(IF(?__event_type = gist:_FinancialBusinessEventType_debt_principal_repayment, 
    IRI(CONCAT(STR(qt3:),'_Magnitude_debtPrincipalRepayment_',?__tx_key,'_',?__netmamt_clean)), 
    ?unbound) AS ?__debt_repayment_magn)


# Currency - CAD or USD
    BIND(IF(?Currency="CAD", qtx:_CADollar_, IF(?Currency="USD", gist:_USDollar_, ?unbound)) AS ?__event_currency)


}